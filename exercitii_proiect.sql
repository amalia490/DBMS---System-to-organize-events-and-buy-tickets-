------------------
-- ex. 6
------------------

CREATE OR REPLACE FUNCTION RAPORT_EVENIMENT (V_EVENIMENT_ID EVENIMENT.EVENIMENT_ID%TYPE)
RETURN NUMBER IS
    NR_PARTICIPANTI NUMBER := 0;
    TYPE vector IS VARRAY(3) OF NUMBER;
    t vector := vector();
    TYPE tablou_imbricat IS TABLE OF PARTICIPANT.PARTICIPANT_ID%TYPE;
    t1 tablou_imbricat := tablou_imbricat();
    TYPE tablou_indexat IS TABLE OF PLATA.STATUS_PLATA%TYPE INDEX BY BINARY_INTEGER;
    t2 tablou_indexat;
    v_index BINARY_INTEGER;
BEGIN
    SELECT TIP_BILET.TIP_BILET_ID BULK COLLECT INTO t
    FROM TIP_BILET
    WHERE TIP_BILET.EVENIMENT_ID = V_EVENIMENT_ID
    ORDER BY TIP_BILET.PRET DESC
    FETCH FIRST 2 ROWS ONLY;

    IF t.COUNT = 2 THEN
        SELECT DISTINCT P.PARTICIPANT_ID BULK COLLECT INTO t1
        FROM PARTICIPANT P
        JOIN BILET B ON P.PARTICIPANT_ID = B.PARTICIPANT_ID
        WHERE B.TIP_BILET_ID IN (t(1), t(2));
    ELSIF t.COUNT = 1 THEN
        SELECT DISTINCT P.PARTICIPANT_ID BULK COLLECT INTO t1
        FROM PARTICIPANT P
        JOIN BILET B ON P.PARTICIPANT_ID = B.PARTICIPANT_ID
        WHERE B.TIP_BILET_ID = t(1);
    END IF;

    IF t1.COUNT > 0 THEN
        FOR i IN 1 .. t1.COUNT LOOP
            BEGIN
                SELECT PL.STATUS_PLATA INTO t2(t1(i))
                FROM PLATA PL
                WHERE PL.PARTICIPANT_ID = t1(i) AND PL.PLATA_ID IN (
                    SELECT B.PLATA_ID
                    FROM BILET B
                    WHERE B.PARTICIPANT_ID = t1(i)
                    AND B.TIP_BILET_ID IN (t(1), t(2))
                    )
                    AND ROWNUM = 1;
            EXCEPTION
                WHEN NO_DATA_FOUND THEN
                    t2(t1(i)) := 'Nicio plata gasita';
            END;
        END LOOP;
    END IF;
    v_index := t2.FIRST;
    WHILE v_index IS NOT NULL LOOP
        IF t2(v_index) = 'Confirmata' THEN
            NR_PARTICIPANTI := NR_PARTICIPANTI + 1;
        END IF;
        v_index := t2.NEXT(v_index);
    END LOOP;
    RETURN NR_PARTICIPANTI;
END;
/

DECLARE
    V_EVENIMENT_ID EVENIMENT.EVENIMENT_ID%TYPE;
    V_EVENIMENT_NUME EVENIMENT.NUME%TYPE;
    NR_PARTICIPANTI NUMBER;
BEGIN
    V_EVENIMENT_ID := &eveniment; 
    DBMS_OUTPUT.PUT_LINE('evenimentul cu ID: ' || V_EVENIMENT_ID);
    NR_PARTICIPANTI := RAPORT_EVENIMENT(V_EVENIMENT_ID);
    SELECT EVENIMENT.NUME INTO V_EVENIMENT_NUME
    FROM EVENIMENT
    WHERE EVENIMENT.EVENIMENT_ID = V_EVENIMENT_ID;
    DBMS_OUTPUT.PUT_LINE('numarul de participanti care au cumparat macar un bilet din primele doua categorii cele mai scumpe pentru evenimentul: ' || V_EVENIMENT_NUME || ' este: ' || NR_PARTICIPANTI);
END;
/   


--------------------
-- ex. 7
--------------------

SET SERVEROUTPUT ON;
CREATE OR REPLACE PROCEDURE PRESTATII
IS 
    v_eveniment_nume EVENIMENT.NUME%TYPE;

    CURSOR c_evenimente IS
        SELECT DISTINCT E.EVENIMENT_ID, E.NUME
        FROM EVENIMENT E
        JOIN CATEGORIE C ON E.CATEGORIE_ID = C.CATEGORIE_ID
        WHERE C.NUME = 'Muzica';

    CURSOR c_prestatii (p_eveniment_id NUMBER) IS
        SELECT PA.PRESTATIE_ID, A.NUME, PA.ROL_ARTIST
        FROM PRESTATIE_ARTIST PA
        JOIN PROGRAM PR ON PA.PROGRAM_ID = PR.PROGRAM_ID
        JOIN EVENIMENT E ON PR.EVENIMENT_ID = E.EVENIMENT_ID
        JOIN ARTIST A ON PA.ARTIST_ID = A.ARTIST_ID
        WHERE E.EVENIMENT_ID = p_eveniment_id;

        TYPE t IS RECORD (
            EVENIMENT_ID EVENIMENT.EVENIMENT_ID%TYPE,
            EVENIMENT_NUME EVENIMENT.NUME%TYPE
        );
        TYPE t_tab IS TABLE OF t;
        v_eveniment t_tab;
        v_gasit_prestatii BOOLEAN;
BEGIN
    OPEN c_evenimente;
    FETCH c_evenimente BULK COLLECT INTO v_eveniment;
    CLOSE c_evenimente;
    IF v_eveniment.COUNT = 0 THEN
        DBMS_OUTPUT.PUT_LINE('Nu s-au gasit evenimente in categoria Muzica.');
        RETURN;
    END IF;
    FOR i IN 1 .. v_eveniment.COUNT LOOP
        DBMS_OUTPUT.PUT_LINE('Eveniment: ' || v_eveniment(i).EVENIMENT_NUME);
        v_gasit_prestatii := FALSE;
        FOR j IN c_prestatii(v_eveniment(i).EVENIMENT_ID) LOOP
            v_gasit_prestatii := TRUE;
            DBMS_OUTPUT.PUT_LINE('  Prestatia ID: ' || j.PRESTATIE_ID || ', Artist: ' || j.NUME || ' ' || ', Rol: ' || j.ROL_ARTIST);
        END LOOP;
        IF NOT v_gasit_prestatii THEN
            DBMS_OUTPUT.PUT_LINE('  (Nu exista prestatii programate pentru acest eveniment)');
        END IF;
    END LOOP;
END;
/

BEGIN
    PRESTATII;
END;
/

--------------------
-- ex. 8
--------------------

CREATE OR REPLACE FUNCTION VENIT_EVENIMENT (V_EVENIMENT_ID EVENIMENT.EVENIMENT_ID%TYPE)
RETURN NUMBER IS
    VENIT_TOTAL NUMBER := 0;
    V_TEST_ID EVENIMENT.EVENIMENT_ID%TYPE;
    V_CAT_ID CATEGORIE.CATEGORIE_ID%TYPE;
    V_NR_TIPURI NUMBER;

    E_FARA_CONFIGURARE EXCEPTION;
BEGIN

    SELECT EVENIMENT_ID, CATEGORIE_ID INTO V_TEST_ID, V_CAT_ID
    FROM EVENIMENT 
    WHERE EVENIMENT_ID = V_EVENIMENT_ID;

    SELECT EVENIMENT_ID INTO V_TEST_ID 
    FROM EVENIMENT 
    WHERE CATEGORIE_ID = V_CAT_ID;

    SELECT COUNT(*) INTO V_NR_TIPURI FROM TIP_BILET WHERE EVENIMENT_ID = V_EVENIMENT_ID;
    IF V_NR_TIPURI = 0 THEN
        RAISE E_FARA_CONFIGURARE;
    END IF;

    SELECT SUM(TP.PRET) INTO VENIT_TOTAL
    FROM BILET B
    JOIN TIP_BILET TP ON B.TIP_BILET_ID = TP.TIP_BILET_ID
    JOIN PLATA P ON B.PLATA_ID = P.PLATA_ID
    WHERE P.STATUS_PLATA = 'Confirmata' AND TP.EVENIMENT_ID = V_EVENIMENT_ID;
    RETURN NVL(VENIT_TOTAL, 0);

    EXCEPTION
        WHEN E_FARA_CONFIGURARE THEN
            RETURN -3;
        WHEN NO_DATA_FOUND THEN
            RETURN -1;
        WHEN TOO_MANY_ROWS THEN
            RETURN -2; 
END;
/

DECLARE
    V_EVENIMENT_ID EVENIMENT.EVENIMENT_ID%TYPE;
    V_EVENIMENT_NUME EVENIMENT.NUME%TYPE;
    VENIT_TOTAL NUMBER;
BEGIN
    V_EVENIMENT_ID := &eveniment;
    DBMS_OUTPUT.PUT_LINE('evenimentul cu ID: ' || V_EVENIMENT_ID);
    VENIT_TOTAL := VENIT_EVENIMENT(V_EVENIMENT_ID);

    IF VENIT_TOTAL >= 0 THEN 
        SELECT EVENIMENT.NUME INTO V_EVENIMENT_NUME
        FROM EVENIMENT
        WHERE EVENIMENT.EVENIMENT_ID = V_EVENIMENT_ID;
        DBMS_OUTPUT.PUT_LINE('venitul total generat de evenimentul: ' || V_EVENIMENT_NUME || ' este: ' || VENIT_TOTAL);
    ELSIF VENIT_TOTAL = -1 THEN
        DBMS_OUTPUT.PUT_LINE('Eroare: Evenimentul cu ID-ul ' || V_EVENIMENT_ID || ' nu exista.');
    ELSIF VENIT_TOTAL = -2 THEN
        DBMS_OUTPUT.PUT_LINE('Eroare: S-au gasit mai multe evenimente cu ID-ul ' || V_EVENIMENT_ID || '.');
    ELSIF VENIT_TOTAL = -3 THEN
        DBMS_OUTPUT.PUT_LINE('Eroare Custom: Evenimentul nu are configurate tipuri de bilete!');
    END IF;
END;
/

--------------------
-- ex. 9    
--------------------

SET SERVEROUTPUT ON;
CREATE OR REPLACE PROCEDURE DETALII_PARTICIPANT (V_PARTICIPANT_ID PARTICIPANT.PARTICIPANT_ID%TYPE, V_EVENIMENT_ID EVENIMENT.EVENIMENT_ID%TYPE) 
IS
    E_PARTICIPANT_FARA_BILET EXCEPTION;
    E_EVENIMENT_FARA_PROGRAM EXCEPTION;
    E_DATE_INCONSISTENTE EXCEPTION;

    TYPE t IS RECORD (
        NUME PARTICIPANT.NUME%TYPE,
        ORA_PARTICIPARE PRESTATIE_ARTIST.ORA_PARTICIPARE%TYPE,
        DURATA_PARTICIPARE PRESTATIE_ARTIST.DURATA_PRESTATIE%TYPE,
        ROL_ARTIST PRESTATIE_ARTIST.ROL_ARTIST%TYPE,
        NUME_ARTIST ARTIST.NUME%TYPE,
        NR_ARTISTI NUMBER
        );
        TYPE t_tab IS TABLE OF t;
        v_prestatie t_tab;
        v_verificare NUMBER;
BEGIN

    SELECT COUNT(*) INTO v_verificare
    FROM BILET B
    JOIN TIP_BILET TB ON B.TIP_BILET_ID = TB.TIP_BILET_ID
    WHERE B.PARTICIPANT_ID = V_PARTICIPANT_ID 
      AND TB.EVENIMENT_ID = V_EVENIMENT_ID;

    IF v_verificare = 0 THEN
        RAISE E_PARTICIPANT_FARA_BILET;
    END IF;

    SELECT COUNT(*) INTO v_verificare
    FROM PROGRAM
    WHERE EVENIMENT_ID = V_EVENIMENT_ID;

    IF v_verificare = 0 THEN
        RAISE E_EVENIMENT_FARA_PROGRAM;
    END IF;

    SELECT DISTINCT P.NUME, PR.ORA_PARTICIPARE, PR.DURATA_PRESTATIE, PR.ROL_ARTIST, A.NUME, 
        (SELECT COUNT(*) 
        FROM PRESTATIE_ARTIST PA2 
        JOIN PROGRAM PROG2 ON PA2.PROGRAM_ID = PROG2.PROGRAM_ID 
        WHERE PROG2.EVENIMENT_ID = V_EVENIMENT_ID)
    BULK COLLECT INTO v_prestatie
    FROM PARTICIPANT P
    JOIN BILET B ON P.PARTICIPANT_ID = B.PARTICIPANT_ID
    JOIN TIP_BILET TB ON B.TIP_BILET_ID = TB.TIP_BILET_ID
    JOIN PROGRAM PROG ON TB.EVENIMENT_ID = PROG.EVENIMENT_ID
    JOIN PRESTATIE_ARTIST PR ON PROG.PROGRAM_ID = PR.PROGRAM_ID
    JOIN ARTIST A ON PR.ARTIST_ID = A.ARTIST_ID
    WHERE P.PARTICIPANT_ID = V_PARTICIPANT_ID AND TB.EVENIMENT_ID = V_EVENIMENT_ID;

    IF v_prestatie.COUNT = 0 THEN
        RAISE E_DATE_INCONSISTENTE;
    END IF;

    FOR i IN 1 .. v_prestatie.COUNT LOOP
        DBMS_OUTPUT.PUT_LINE('Participant: ' || v_prestatie(i).NUME);
        DBMS_OUTPUT.PUT_LINE('  Nume Artist: ' || v_prestatie(i).NUME_ARTIST || ', Ora Participare: ' || TO_CHAR(v_prestatie(i).ORA_PARTICIPARE, 'HH24:MI') || ', Durata: ' || v_prestatie(i).DURATA_PARTICIPARE || ', Rol: ' || v_prestatie(i).ROL_ARTIST);
    END LOOP;

    EXCEPTION
    WHEN E_PARTICIPANT_FARA_BILET THEN
        DBMS_OUTPUT.PUT_LINE('EROARE: Participantul ' || V_PARTICIPANT_ID || ' nu detine un bilet valid pentru evenimentul ' || V_EVENIMENT_ID);
    
    WHEN E_EVENIMENT_FARA_PROGRAM THEN
        DBMS_OUTPUT.PUT_LINE('EROARE: Evenimentul ' || V_EVENIMENT_ID || ' nu are nicio prestatie artistica inregistrata in program.');
    
    WHEN E_DATE_INCONSISTENTE THEN
        DBMS_OUTPUT.PUT_LINE('EROARE: Nu s-au putut corela datele despre prestatii pentru acest bilet (posibile date lipsa in tabelele de legatura).');

    WHEN OTHERS THEN
        DBMS_OUTPUT.PUT_LINE('EROARE NEASTEPTATA: ' || SQLERRM);
END;
/

DECLARE
    V_EVENIMENT_ID EVENIMENT.EVENIMENT_ID%TYPE;
    V_PARTICIPANT_ID PARTICIPANT.PARTICIPANT_ID%TYPE;
BEGIN
    V_EVENIMENT_ID := &eveniment;
    V_PARTICIPANT_ID := &participant;
    DBMS_OUTPUT.PUT_LINE('evenimentul cu ID: ' || V_EVENIMENT_ID || ' si participantul cu ID: ' || V_PARTICIPANT_ID);
    DETALII_PARTICIPANT(v_participant_id, v_eveniment_id);
END;
/


------------------
-- ex. 10
------------------

--trigger de tip LMD la nivel de comanda 

CREATE OR REPLACE TRIGGER trg_orar
BEFORE INSERT OR UPDATE OR DELETE ON EVENIMENT
DECLARE
    v_zi VARCHAR2(20);
    v_ora          NUMBER;
BEGIN
    v_zi := TRIM(TO_CHAR(SYSDATE, 'DAY', 'NLS_DATE_LANGUAGE = ROMANIAN'));
    v_ora := TO_CHAR(SYSDATE, 'HH24');

    IF v_zi IN ('DUMINICĂ', 'LUNI') THEN
        RAISE_APPLICATION_ERROR(-20001, 'Operatiile pe tabelul EVENIMENT sunt interzise duminica si luni');
    ELSIF v_ora < 8 OR v_ora >= 16 THEN
        RAISE_APPLICATION_ERROR(-20002, 'Accesul la date este permis doar intre orele 08:00 si 17:00.');
    ELSE
        DBMS_OUTPUT.PUT_LINE('Comanda acceptata la data: ' || TO_CHAR(SYSDATE, 'DD-MON-YYYY HH24:MI'));
    END IF;
END;
/


INSERT INTO EVENIMENT (EVENIMENT_ID, NUME, DESCRIERE, DATA_INCEPUT, DATA_FINAL, LOCATIE_ID, CATEGORIE_ID) VALUES 
(3, 'Festival de dans', 'Fara descriere inca, mai multe detalii vor fi puse la dispozitie ulterior.', TO_DATE('2025-07-10', 'YYYY-MM-DD'), TO_DATE('2025-07-12', 'YYYY-MM-DD'), 25, 19);

DROP TRIGGER trg_orar;


--------------
-- ex. 11
--------------

--trigger de tip LMD la nivel de rand

CREATE OR REPLACE TRIGGER trg_TIP_BILET
BEFORE UPDATE OF PRET ON TIP_BILET
FOR EACH ROW
BEGIN
    IF :NEW.PRET < :OLD.PRET THEN
        RAISE_APPLICATION_ERROR(-20003, 'Pretul biletului nu poate fi redus.');
    END IF;
END;
/

UPDATE TIP_BILET SET PRET = PRET - 10 WHERE TIP_BILET_ID = 19;

DROP TRIGGER trg_TIP_BILET;

------------------
-- ex. 12
------------------

--trigger de tip LDD

CREATE TABLE AUDIT_P(
    EVENIMENT VARCHAR2(10),
    NUME_BAZADATE VARCHAR2(30),
    DATA_MODIFICARE DATE,
    UTILIZATOR VARCHAR2(30)
);

CREATE OR REPLACE TRIGGER trg_validare_proiect 
AFTER CREATE OR DROP OR ALTER ON SCHEMA
BEGIN
    INSERT INTO AUDIT_P (EVENIMENT, NUME_BAZADATE, DATA_MODIFICARE, UTILIZATOR)
    VALUES (
        CASE 
            WHEN ORA_SYSEVENT = 'CREATE' THEN 'CREARE'
            WHEN ORA_SYSEVENT = 'DROP' THEN 'STERGERE'
            WHEN ORA_SYSEVENT = 'ALTER' THEN 'MODIFICARE'
        END,
        SYS.DATABASE_NAME,
        SYSDATE,
        SYS.LOGIN_USER
    );
END;
/

CREATE TABLE TEST_TBL (
    ID NUMBER PRIMARY KEY,
    DATE_TEST VARCHAR2(100)
);

DROP TABLE TEST_TBL;
SELECT * FROM AUDIT_P;

DROP TRIGGER trg_validare_proiect;

------------------
-- EX. 13
------------------

CREATE OR REPLACE PACKAGE PACHET AS 

    TYPE DETALII_PLATA IS RECORD(
        V_PLATA_ID PLATA.PLATA_ID%TYPE,
        V_SUMA_TOTALA PLATA.SUMA_TOTALA%TYPE,
        V_METODA_PLATA PLATA.METODA_PLATA%TYPE
    );
    TYPE LISTA_PLATI IS TABLE OF DETALII_PLATA;

    TYPE LISTA_PLATI_SUMA IS RECORD (
        V_EVENIMENT_NUME EVENIMENT.NUME%TYPE,
        COLECTIE_PLATI LISTA_PLATI,
        SUMA_INCS NUMBER
    );

    TYPE TABLOU_LISTA_PLATI IS TABLE OF LISTA_PLATI_SUMA;

    TYPE TIP_BILETE_RAPORT IS RECORD (
        V_PRET_NOU TIP_BILET.PRET%TYPE,
        V_PRET_VECHI TIP_BILET.PRET%TYPE
    );
    TYPE TABLOU_RAPORT IS TABLE OF TIP_BILETE_RAPORT INDEX BY BINARY_INTEGER;

    V_CATEGORIE_NUME CATEGORIE.NUME%TYPE;

    PROCEDURE PROCEDURA_PLATI (V_CATEGORIE_NUME CATEGORIE.NUME%TYPE);

    FUNCTION SUMA_INCASATA_EVENIMENT (
        V_EVENIMENT_ID EVENIMENT.EVENIMENT_ID%TYPE
    ) RETURN NUMBER;

    PROCEDURE ACTUALIZARE_PRET_BILET (
        PROCENT NUMBER
    );

    FUNCTION PROCENT_BILETE_VANDUTE (
        V_EVENIMENT_ID EVENIMENT.EVENIMENT_ID%TYPE
    ) RETURN NUMBER;

    

END PACHET;
/

CREATE OR REPLACE PACKAGE BODY PACHET AS
    PROCEDURE PROCEDURA_PLATI(V_CATEGORIE_NUME CATEGORIE.NUME%TYPE) IS
        T_LISTA_PLATI TABLOU_LISTA_PLATI := TABLOU_LISTA_PLATI();
        V_SUMA_INCASATA NUMBER;
        V_ULTIMUL NUMBER;
    BEGIN 
        FOR i IN (
            SELECT E.EVENIMENT_ID, E.NUME
            FROM EVENIMENT E
            JOIN CATEGORIE C ON C.CATEGORIE_ID = E.CATEGORIE_ID
            WHERE C.NUME = V_CATEGORIE_NUME
        ) LOOP
            T_LISTA_PLATI.EXTEND;
            V_ULTIMUL := T_LISTA_PLATI.COUNT;
            SELECT P.PLATA_ID, P.SUMA_TOTALA, P.METODA_PLATA
            BULK COLLECT INTO T_LISTA_PLATI(V_ULTIMUL).COLECTIE_PLATI
                FROM PLATA P
                JOIN BILET B ON B.PLATA_ID = P.PLATA_ID
                JOIN TIP_BILET TB ON TB.TIP_BILET_ID = B.TIP_BILET_ID
                WHERE TB.EVENIMENT_ID = i.EVENIMENT_ID;
            T_LISTA_PLATI(V_ULTIMUL).SUMA_INCS := PACHET.SUMA_INCASATA_EVENIMENT(i.EVENIMENT_ID);
            T_LISTA_PLATI(V_ULTIMUL).V_EVENIMENT_NUME := I.NUME;
        END LOOP;
        
    FOR i IN 1..T_LISTA_PLATI.COUNT LOOP
        DBMS_OUTPUT.PUT_LINE('EVENIMENT: ' || T_LISTA_PLATI(i).V_EVENIMENT_NUME || ', SUMA INCASATA: ' || T_LISTA_PLATI(i).SUMA_INCS);
        FOR j IN 1..T_LISTA_PLATI(i).COLECTIE_PLATI.COUNT LOOP
            DBMS_OUTPUT.PUT_LINE('PLATA ID: ' || T_LISTA_PLATI(i).COLECTIE_PLATI(j).V_PLATA_ID || ', SUMA TOTALA: ' || T_LISTA_PLATI(i).COLECTIE_PLATI(j).V_SUMA_TOTALA || ', METODA PLATA: ' || T_LISTA_PLATI(i).COLECTIE_PLATI(j).V_METODA_PLATA);
        END LOOP;
    END LOOP;
END PROCEDURA_PLATI;

    FUNCTION SUMA_INCASATA_EVENIMENT (
        V_EVENIMENT_ID EVENIMENT.EVENIMENT_ID%TYPE
    ) RETURN NUMBER IS
        SUMA_TOTALA NUMBER := 0;
    BEGIN 
        SELECT SUM(TB.PRET) INTO SUMA_TOTALA
        FROM TIP_BILET TB
        JOIN BILET B ON B.TIP_BILET_ID = TB.TIP_BILET_ID
        WHERE TB.EVENIMENT_ID = V_EVENIMENT_ID
        AND B.PLATA_ID IN (
            -- Luăm doar ID-urile de plăți care sunt confirmate
            SELECT PLATA_ID 
            FROM PLATA 
            WHERE STATUS_PLATA = 'Confirmata'
        );
        RETURN NVL(SUMA_TOTALA, 0);
    END SUMA_INCASATA_EVENIMENT;

    PROCEDURE ACTUALIZARE_PRET_BILET (
        PROCENT NUMBER
    ) IS
    TABLOU TABLOU_RAPORT;
    v_index NUMBER;
    v_pret_nou NUMBER;
    BEGIN
        FOR i IN (
            SELECT TB.TIP_BILET_ID, TB.PRET
            FROM TIP_BILET TB
        ) LOOP
        TABLOU(i.TIP_BILET_ID).V_PRET_VECHI := i.PRET;
        v_pret_nou := i.PRET * (1 + PROCENT / 100);

        UPDATE TIP_BILET
        SET PRET = v_pret_nou
        WHERE TIP_BILET_ID = i.TIP_BILET_ID;

        TABLOU(i.TIP_BILET_ID).V_PRET_NOU := v_pret_nou;
        END LOOP;
    v_index := TABLOU.FIRST;
    WHILE v_index IS NOT NULL LOOP
        DBMS_OUTPUT.PUT_LINE('TIP BILET ID: ' || v_index || ', PRET VECHI: ' || TABLOU(v_index).V_PRET_VECHI || ', PRET NOU: ' || TABLOU(v_index).V_PRET_NOU);
        v_index := TABLOU.NEXT(v_index);
    END LOOP;
    END ACTUALIZARE_PRET_BILET;

    FUNCTION PROCENT_BILETE_VANDUTE (
        V_EVENIMENT_ID EVENIMENT.EVENIMENT_ID%TYPE
    ) RETURN NUMBER IS
        TOTAL_BILETE NUMBER := 0;
        BILETE_VANDUTE NUMBER := 0;
        PROCENT NUMBER := 0;
    BEGIN
        SELECT NVL(SUM(LOCURI_DISPONIBILE), 0) INTO TOTAL_BILETE
        FROM TIP_BILET
        WHERE EVENIMENT_ID = V_EVENIMENT_ID;

        SELECT COUNT(*) INTO BILETE_VANDUTE
        FROM BILET B
        JOIN TIP_BILET TB ON B.TIP_BILET_ID = TB.TIP_BILET_ID
        JOIN PLATA P ON B.PLATA_ID = P.PLATA_ID
        WHERE P.STATUS_PLATA = 'Confirmata' AND TB.EVENIMENT_ID = V_EVENIMENT_ID;

        IF TOTAL_BILETE = 0 THEN
            RETURN 0;
        ELSE
            PROCENT := (BILETE_VANDUTE / TOTAL_BILETE) * 100;
            RETURN PROCENT;
        END IF;
    END PROCENT_BILETE_VANDUTE;

END PACHET;
/

DECLARE
    V_PROCENT NUMBER;
    V_EVENIMENT_ID EVENIMENT.EVENIMENT_ID%TYPE := 28;
    BEGIN
        PACHET.PROCEDURA_PLATI('Muzica');

        V_PROCENT := PACHET.PROCENT_BILETE_VANDUTE(
            V_EVENIMENT_ID => V_EVENIMENT_ID
        );

        PACHET.ACTUALIZARE_PRET_BILET(
            PROCENT => 30
        );
        ROLLBACK;
        DBMS_OUTPUT.PUT_LINE('Procentul de bilete vandute pentru evenimentul cu ID ' || V_EVENIMENT_ID || ' este: ' || TO_CHAR(V_PROCENT, '990.99') || '%');

    END;
/

